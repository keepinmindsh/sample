plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
    id 'java'
}

repositories {
    maven { url "https://repo.spring.io/milestone" }
    mavenCentral()
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.ewerk.gradle.plugins.querydsl'

    repositories {
        mavenCentral()
    }

    dependencyManagement {

        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.3.1.RELEASE"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.RELEASE"
        }

        dependencies {

        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        implementation 'com.querydsl:querydsl-jpa'
        implementation 'com.querydsl:querydsl-apt'

        implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.4.3'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'

    def querydslDir = "$buildDir/generated/querydsl"

    querydsl {
        library = "com.querydsl:querydsl-apt"
        jpa = true
        querydslSourcesDir = querydslDir
    }

    sourceSets {
        main.java.srcDir querydslDir
    }

    configurations {
        querydsl.extendsFrom compileClasspath
    }

    compileQuerydsl {
        options.annotationProcessorPath = configurations.querydsl
    }
}

project(":lines-starter"){

    bootJar {
        mainClassName = 'lines.LinesModularMonolithApplication'
        archiveFileName = "lines_starter.jar"
    }

    jar {
        enabled = true;
    }

    dependencies {
        implementation project(path :":lines-sample-api", configuration: 'default')

        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        implementation files("../libs/m_linesconnection.jar")
        implementation 'org.apache.commons:commons-dbcp2'
        implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.4.3'
        implementation 'org.mariadb.jdbc:mariadb-java-client'
    }
}

project(":lines-sample-api"){

    bootJar {
        enabled = false;
    }

    jar {
        enabled = true;
    }

    dependencies {
        implementation files("../libs/m_linesconnection.jar")
        implementation 'org.apache.commons:commons-dbcp2'
        implementation 'org.mariadb.jdbc:mariadb-java-client'
    }
}
